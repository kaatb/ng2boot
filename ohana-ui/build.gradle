import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'com.moowork.node'

buildscript {
    repositories {
        mavenCentral()
        jcenter() // gradle-node-plugin is released on JFrog
    }

    dependencies {
        classpath "com.moowork.gradle:gradle-node-plugin:0.13"
    }
}

idea {
    module {
        excludeDirs += file('node_modules')
        downloadSources = true
    }
}

node {
    // Version of node to use.
    version = '7.2.0'

    // Version of npm to use.
//    npmVersion = '2.1.5'

    // Base URL for fetching node distributions (change if you have a mirror).
    distBaseUrl = 'https://nodejs.org/dist'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true

    // Set the work directory for unpacking node
    workDir = file("${project.buildDir}/nodejs")

    // Set the work directory where node_modules should be located
    nodeModulesDir = file("${project.projectDir}")
}

npmInstall {
    workingDir = file('./src')
    outputs.upToDateWhen {false} // trick gradle into always running npm install to add new dependencies
}

assemble.finalizedBy 'ngBuildProd'

test.dependsOn 'ngTest'

task ngTest(type: NpmTask) {
    workingDir = file('./src')
    args = ['run', 'ngSingleTest']
}

task ngBuildProd(type: NpmTask) {
    workingDir = file('./src')
    args = ['run', 'buildProd']
}

war {
    from 'src/dist'
}